import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

repositories {
    maven {
        name "jitpack-repository"
        url "https://jitpack.io/"
    }

    maven {
        name "okaeri-repository"
        url "https://storehouse.okaeri.eu/repository/maven-public/"
    }

    maven {
        name "placeholderapi-repository"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    implementation(project(":spigot"))
    implementation(project(":v1_8"))
    implementation(project(":v1_9"))
    implementation(project(":v1_12"))

    compileOnly "org.spigotmc:spigot-api:1.8-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.1"

    implementation "eu.okaeri:okaeri-configs-yaml-bukkit:4.0.5"
    implementation "eu.okaeri:okaeri-configs-validator-okaeri:4.0.4"
    implementation "com.github.Querz:NBT:6.1"
    implementation "org.slf4j:slf4j-reload4j:1.7.36"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "org.bstats:bstats-bukkit:3.0.0"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
}

test {
    useJUnitPlatform()

    testLogging {
        events(TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED)
        exceptionFormat = TestExceptionFormat.FULL
        showExceptions = true
        showCauses = true
        showStackTraces = true
        showStandardStreams = true
    }
}

java {
    withSourcesJar()
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

shadowJar {
    archiveFileName = "${project.parent.name} v${project.version}.jar"

    relocate "org.bstats", "io.github.zrdzn.minecraft.greatlifesteal.libs.bstats"
}
